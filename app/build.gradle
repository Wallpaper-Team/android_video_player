plugins {
    id 'com.android.library'
}

android {
    namespace 'com.brouken.player'
    compileSdkVersion 33
    ndkVersion '21.4.7075529'

    defaultConfig {
        //applicationId "com.brouken.player"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 121
        versionName "0.121"
        archivesBaseName = "Just.Player.v${versionName}"
    }

    buildTypes {
        release {
            minifyEnabled false
            //proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "targetSdk", "distribution"
    productFlavors {
        latest {
            dimension "targetSdk"
        }
        legacy {
            dimension "targetSdk"
            //applicationIdSuffix ".legacy"
            versionNameSuffix "-legacy"
            targetSdkVersion 29
        }

        universal {
            dimension "distribution"
        }
        amazon {
            dimension "distribution"
        }
    }
    variantFilter { variant ->
        def names = variant.flavors*.name
        if (names.contains("legacy") && names.contains("amazon")) {
            setIgnore(true)
        }
    }
    androidComponents {
        onVariants(selector().withFlavor(new kotlin.Pair("distribution", "amazon")), {
            //packaging.dex.useLegacyPackaging.set(false)
            packaging.jniLibs.excludes.add('lib/armeabi-v7a/*_neon.so')
        })
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lint {
        disable 'MissingTranslation'
    }
}

dependencies {
    def exoplayerVersion = '2.18.0'
    implementation("com.google.android.exoplayer:exoplayer-datasource:$exoplayerVersion") {
        exclude group: "com.google.android.exoplayer", module: "exoplayer-common"
    }
    implementation("com.google.android.exoplayer:exoplayer-decoder:$exoplayerVersion") {
        exclude group: "com.google.android.exoplayer", module: "exoplayer-common"
    }
    implementation("com.google.android.exoplayer:exoplayer-database:$exoplayerVersion") {
        exclude group: "com.google.android.exoplayer", module: "exoplayer-common"
    }
    implementation("com.google.android.exoplayer:extension-mediasession:$exoplayerVersion") {
        exclude group: "com.google.android.exoplayer", module: "exoplayer-common"
    }
    implementation("com.google.android.exoplayer:exoplayer-dash:$exoplayerVersion") {
        exclude group: "com.google.android.exoplayer", module: "exoplayer-core"
    }
    implementation("com.google.android.exoplayer:exoplayer-hls:$exoplayerVersion") {
        exclude group: "com.google.android.exoplayer", module: "exoplayer-core"
    }
    implementation("com.google.android.exoplayer:exoplayer-smoothstreaming:$exoplayerVersion") {
        exclude group: "com.google.android.exoplayer", module: "exoplayer-core"
    }
    implementation("com.google.android.exoplayer:exoplayer-rtsp:$exoplayerVersion") {
        exclude group: "com.google.android.exoplayer", module: "exoplayer-core"
    }
    api('com.google.guava:guava:31.0.1-android') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'org.checkerframework', module: 'checker-compat-qual'
        exclude group: 'org.checkerframework', module: 'checker-qual'
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
        exclude group: 'com.google.j2objc', module: 'j2objc-annotations'
        exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
    }
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.13.3'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    implementation 'com.ibm.icu:icu4j:71.1'
    implementation 'com.arthenica:ffmpeg-kit-https:4.5.1-1.LTS'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation project(path: ':doubletapplayerview')
    api project(':app:ui')
    api project(':app:common')
    api project(':app:core')
    api project(':app:extractor')
    api project(':app:av1')
    api project(':app:ffmpeg')
}